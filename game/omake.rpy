label omake:
    scene black with fade
    "In Japan, \"omake\" are extra or deleted scenes for a show or game."
    "So here are some omake from {i}Our Personal Space{/i}."
    
    scene bg path with fade
    show brennan with dissolve
    
    brennan "Nothing like a nice bath after a hard day's work at the office... Hopefully no one's using it."
    
    scene bg bathhouse with fade
    show overlay bathhouse
    show her serious at midright, squatting behind overlay
    show him serious at midleft, squatting behind overlay
    with dissolve
    
    her normal "So then he says, \"I'd never forget a pretty face like yours.\""
    him surprised "Seriously!?  What a cheeseball!"
    her annoyed "I know! Like I'm supposed to feel flattered because some random guy likes my physical appearance."
    him happy "And you've already got me, so what more could you want in a man?"
    her flirting "I don't know, maybe more flattering cheesy lines?"
    him serious "Ohhh, [her_nickname], you're so sexy, you make my heart race faster than a space shuttle."
    her serious "You're so sexy, you're illegal to bring on space shuttles."
    him laughing "Ha ha ha!"
    her laughing "Ha ha ha ha ha!"
    
    brennan "..."
    
    scene bg path with fade
    show brennan with dissolve
    brennan "There go my plans for a bath."
    brennan "Maybe I'll see if Jed's busy..."
    
    scene bg library with fade
    show jed at midleft
    show helen at midright, squatting
    with dissolve
    jed "You're the Milky Way in my night sky, baby."
    helen "Is that a nursing joke?"
    jed "Nah, I was just tryin' to be romantic."
    helen "Sorry, it's hard for me to think about anything else while I'm feeding the baby. Can we be romantic when I'm done?"
    jed "I'll just have to find some other way to help get you in the mood..."
    show jed at squatting with move
    helen "Ohhh. Mmmmm. Oh, Jed, that feels sooo good."
    jed "You never could resist a good foot rub."
    
    brennan "..."
    
    scene bg path with fade
    show brennan with dissolve
    brennan "Sounds like he's busy."
    brennan "Maybe I'll just head home."
    
    scene bg farm_interior flip with fade
    show pavel at midleft    
    show naomi at midright
    with dissolve
    
    pavel "Priya, have you seen my computer around here?"
    naomi "Yes, it's right here, but I wanted to speak with you first."
    pavel "What is it?"
    naomi "That was very kind of you, what you did for Miranda today."
    pavel "What? I just asked her to see if she would mind babysitting for our new parents once in a while."
    naomi "But it helped her feel like you trusted her, as an adult member of the community. That is just what she needed."
    pavel "Well, good. I'm just trying to help everyone..."
    naomi "I know you are, beloved. That's one of the things I love about you."
    pavel "I couldn't do it without you, priya."
    show pavel at center with move
    
    brennan "..."
    
    scene bg sunset with fade
    show brennan at midleft with dissolve
    brennan "I don't want to interrupt... they don't have that many romantic moments as it is."
    brennan "At least there's a beautiful sunset."
    
    brennan "..."
    show lily at midright with moveinright
    lily "Brennan?"
    brennan "Dr. Lily! It's so pleasant to see someone who--"
    lily upset "Someone who what?"
    brennan "Someone who is not busy snogging."
    lily normal "Oh. No, I have not been doing that."
    brennan "What brings you this way?"
    lily upset "It is the anniversary of Winston's death. I found some flowers, and thought I might place them on his grave."
    brennan "Winston was your husband, right?"
    lily normal "Yes."
    brennan "...I see. Well, don't let me stop you."
    lily "I won't."
    hide lily with moveoutleft
    brennan "...Bye."
    brennan "Even Dr. Lily has someone..."
    brennan "..."
    brennan "Who's there?!"
    
    show lettie at right with moveinright
    brennan "Lettie?"
    play sound "sfx/whinny.mp3"
    brennan "Did I ever tell you what a good-looking horse you are?"
    play sound "sfx/horse-snort.mp3"
    brennan "I haven't? Well, just look at those long, straight legs! And that wavy mane!"
    play sound "sfx/whinny.mp3"
    brennan "I know, no one appreciates me, either."
    
    scene black with fade
    
    "End .:."
    return